# $Id$

/[\r\n\t ]+/;
"#" .. "\n";

"'" .. "'" -> $str {
	# XXX: would be /.+/ here but this incorrectly includes /exit/
	/./ -> $char;
}

'"' .. '"' -> $str {
	'\t'    -> $esc;
	'\n'    -> $esc;
	'\v'    -> $esc;
	'\r'    -> $esc;
	'\f'    -> $esc;
	'\\'    -> $esc;
	/[^\\]/ -> $char;
}

'/' .. '/' /[ir]*/ -> $re {
	/./ -> $char;
}

'='  -> $bind;
';'  -> $semi;
'..' -> $to;
'->' -> $map;

'{' -> $open;   '}' -> $close;
'(' -> $lparen; ')' -> $rparen;

# regexp operators
'*' -> $star;
'+' -> $cross;
'?' -> $qmark;

# expression operators
# TODO: complete, intersection
'~' -> $tilde;
'-' -> $dash;
'!' -> $bang;
'.' -> $dot;
'|' -> $pipe;

ident = /[a-z_][a-z0-9_]*/i;

'$' ident -> $token;
ident -> $ident;

