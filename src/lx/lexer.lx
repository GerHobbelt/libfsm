# $Id$

/[\r\n\t ]+/;
"#" .. "\n";

"'" .. "'" -> $str {
	# XXX: would be /.+/ here but this incorrectly includes /exit/
	/./ -> $char;
}

'"' .. '"' -> $str {
	'\\' -> $esc;
	'\"' -> $esc;
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;
	'\v' -> $esc;

	'\'  /[0-7]+/     -> $oct;
	'\x' /[0-9a-f]+/i -> $hex;

	/[^\\]/ -> $char;
}

'/' .. '/' /[a-z]*/i -> $re {
	/./ -> $char;
}

'='  -> $bind;
';'  -> $semi;
'..' -> $to;
'->' -> $map;

'{' -> $open;   '}' -> $close;
'(' -> $lparen; ')' -> $rparen;

# regexp operators
'*' -> $star;
'+' -> $cross;
'?' -> $qmark;

# expression operators
# TODO: complete, intersection
'~' -> $tilde;
'-' -> $dash;
'\' -> $dash; # synonym for '-'
'!' -> $bang;
'.' -> $dot;
'|' -> $pipe;

ident = /[a-z_][a-z0-9_]*/i;

'$' ident -> $token;
ident -> $ident;

