/* $Id$ */

%types%

	char;
	cflags;
	string;
	fsm;
	zone;
	ast;

%terminals%

	TOKEN: () -> (:string);
	IDENT: () -> (:string);

	ESC:  () -> (:char);
	CHAR: () -> (:char);

	RE: () -> (:cflags);
	STR;

	BIND;
	SEMI;
	TO;
	MAP;

	CLOSE;  OPEN;
	LPAREN; RPAREN;

	STAR;
	CROSS;
	QMARK;

	TILDE;
	DASH;
	BANG;
	DOT;
	PIPE;

	EOF;
	!ERROR;
	!UNKNOWN;

%productions%

	<pattern-char>:   (:char) -> ();
	<pattern-buffer>: ()      -> (:string);

	<deref-var>:       (:zone, :string) -> (:fsm);
	<deref-token>:     (:zone, :string) -> (:fsm);

	<compile-literal>: (:string)          -> (:fsm);
	<compile-regex>:   (:string, :cflags) -> (:fsm);

	!<regex-empty>: () -> (:fsm); /* TODO: proper name for matching the empty string? */
	<regex-any>:    () -> (:fsm);

	<op-star>:        (:fsm) -> ();
	<op-cross>:       (:fsm) -> ();
	<op-qmark>:       (:fsm) -> ();
	<op-complement>:  (:fsm) -> ();
	!<op-complete>:   (:fsm) -> ();
	<op-reverse>:     (:fsm) -> ();

	<op-concat>:      (:fsm, :fsm) -> (:fsm);
	<op-product>:     (:fsm, :fsm) -> (:fsm);
	<op-subtract>:    (:fsm, :fsm) -> (:fsm);
	<op-alt>:         (:fsm, :fsm) -> (:fsm);

	<subtract-exit>:  (:fsm, :fsm) -> (:fsm);

	<make-ast>:     ()     -> (:ast);
	<make-zone>:    (:ast) -> (:zone);

	<add-mapping>: (:ast, :zone, :zone, :fsm, :string) -> ();
	<add-binding>: (:ast, :zone, :string, :fsm)        -> ();

	<set-globalzone>: (:ast, :zone) -> ();

	<no-token>: () -> (:string);
	<no-exit>:  () -> (:fsm);
	<no-to>:    () -> (:zone);	/* TODO: rename target? */

	<clone>: (:fsm) -> (:fsm);
	!<free>: (:string) -> ();

	<err-syntax>;
	!<err-expected-alt>;
	<err-expected-map>;
	<err-expected-bind>;
	<err-expected-semi>;
	<err-expected-to>;
	<err-expected-open>;
	<err-expected-close>;
	<err-expected-eof>;

	<err-expected-thing>;
	<err-expected-list>;

	pattern: (z :zone) -> (r :fsm) [

		body: () -> () = {
			c = ESC;
			<pattern-char>(c);
			body;
		||
			c = CHAR;
			<pattern-char>(c);
			body;
		||
			$;
		};

	] = {
		n = IDENT;
		r = <deref-var>(z, n);
	||
		t = TOKEN;
		r = <deref-token>(z, t);
	||
		body;
		STR;
		s = <pattern-buffer>;
		r = <compile-literal>(s);
	||
		body;
		f = RE;
		s = <pattern-buffer>;
		r = <compile-regex>(s, f);
	};

	expr: (z :zone) -> (q :fsm) [

		primary-expr: (z :zone) -> (q :fsm) = {
			q = pattern(z);
		||
			LPAREN;
			q = expr(z);
			RPAREN;
		};

		postfix-expr: (z :zone) -> (q :fsm) = {
			q = primary-expr(z);
		||
			q = postfix-expr(z);
			STAR;
			<op-star>(q);
		||
			q = postfix-expr(z);
			CROSS;
			<op-cross>(q);
		||
			q = postfix-expr(z);
			QMARK;
			<op-qmark>(q);
		};

		prefix-expr: (z :zone) -> (q :fsm) = {
			q = postfix-expr(z);
		||
			TILDE;
			q = prefix-expr(z);
			<op-complement>(q);
		||
			BANG; /* unary minus might make more sense here */
			q = prefix-expr(z);
			<op-reverse>(q);
		};

		infix-expr: (z :zone) -> (q :fsm) [

			dot-expr: (z :zone) -> (q :fsm) = {
				q = prefix-expr(z);
			||
				a = prefix-expr(z);
				DOT;
				b = dot-expr(z);
				q = <op-product>(a, b);
			};

			cat-expr: (z :zone) -> (q :fsm) = {
				q = dot-expr(z);
			||
				a = dot-expr(z);
				/* no operator */
				b = cat-expr(z);
				q = <op-concat>(a, b);
			};

			sub-expr: (z :zone) -> (q :fsm) = {
				q = cat-expr(z);
			||
				a = cat-expr(z);
				DASH;
				b = sub-expr(z);
				q = <op-subtract>(a, b);
			};

			alt-expr: (z :zone) -> (q :fsm) = {
				q = sub-expr(z);
			||
				a = alt-expr(z);
				PIPE;
				b = sub-expr(z);
				q = <op-alt>(a, b);
			};

		] = {
			q = alt-expr(z);
		};

	] = {
		q = infix-expr(z);
	};

	token-mapping: (z :zone) -> (r :fsm, t :string) = {
		r = expr(z);

		{
			t = <no-token>;
		||
			{
				MAP;
			##
				<err-expected-map>;
			};

			t = TOKEN;
		};
	};

	list-of-things: (a :ast, z :zone, exit :fsm) -> () [

		token-thing: (a :ast, z :zone, exit :fsm) -> (q :fsm, t :string) = {
			(r, t) = token-mapping(z);

			q = <subtract-exit>(r, exit);

			{
				SEMI;
			##
				<err-expected-semi>;
			};
		};

		bind-thing: (a :ast, z :zone, exit :fsm) -> () = {
			n = IDENT;

			{
				BIND;
			##
				<err-expected-bind>;
			};

			r = expr(z);
			q = <subtract-exit>(r, exit);

			{
				SEMI;
			##
				<err-expected-semi>;
			};

			<add-binding>(a, z, n, q);
		};

		zone-thing: (a :ast, parent :zone, exit :fsm) -> () = {
			(r1, t1) = token-mapping(parent);

			q1 = <subtract-exit>(r1, exit);

			{
				TO;
			##
				<err-expected-to>;
			};

			(r2, t2) = token-mapping(parent);

			child = <make-zone>(a);

			{
				{
					SEMI;
				##
					<err-expected-semi>;
				};

				/*
				 * This is an implicit zone, written: /r1/ .. /r2/;
				 * which is equivalent to: /r1/ .. /r2/ { /./ - /r2/; }
				 * (i.e. discard everything which is not the exiting pattern.)
				 *
				 * If you want a zone where every character (other than the
				 * r2ing pattern) is treated as an error, then explicitly
				 * specify an empty zone instead: /r1/ .. /r2/ { }
				 */

				x = <no-to>;
				y = <no-token>;

				/*
				 * The exit pattern takes precedence over the zone body because
				 * it matches the shortest expression, rather than the usual
				 * greedy matching (which could traverse out of its accept state).
				 *
				 * This is easier to see visually; there are no transitions out
				 * of the exit pattern's accepting states because the result of
				 * subtraction is trimmed of dead states.
				 */

				u = <clone>(r2);
				w = <regex-any>;
				v = <subtract-exit>(w, u);

				<add-mapping>(a, child, x, v, y);
			||
				{
					OPEN;
				##
					<err-expected-open>;
				};

				list-of-things(a, child, r2);

				{
					CLOSE;
				##
					<err-expected-close>;
				};
			##
				<err-expected-list>;
			};

			<add-mapping>(a, parent, child, q1, t1);
			<add-mapping>(a, child, parent, r2, t2);
		};

		/*
		 * All "things" within a zone have the parent zone's exit pattern
		 * subtracted from them (for an inner zone, the entry pattern
		 * has its parent's exit pattern subtracted).
		 *
		 * This is done just for user convenience; given that matching the
		 * exit pattern within a zone is illegal anyway (due to conflict),
		 * it is reasonable to write (for example) /./ and have the exit
		 * pattern excluded from that.
		 */

		thing: (a :ast, z :zone, exit :fsm) -> () = {
			(r, t) = token-thing(a, z, exit);
			to = <no-to>;
			<add-mapping>(a, z, to, r, t);
		||
			bind-thing(a, z, exit);
		||
			zone-thing(a, z, exit);
		##
			<err-expected-thing>;
		};

	] = {
		thing(a, z, exit);
	||
		list-of-things(a, z, exit);
		thing(a, z, exit);
	##
		<err-expected-list>;
	};

	lx: () -> (a :ast) = {
		a = <make-ast>;
		z = <make-zone>(a);

		exit = <no-exit>;

		<set-globalzone>(a, z);

		list-of-things(a, z, exit);

		{
			EOF;
		##
			<err-expected-eof>;
		};
	##
		a = <make-ast>;

		<err-syntax>;
	};

%entry%

	lx;

