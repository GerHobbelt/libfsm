# $Id$

'('  -> $opensub;
')'  -> $closesub;

# predicates
'^'  -> $start;
'$'  -> $end;

# operators
'?'  -> $qmark;
'*'  -> $star;
'+'  -> $plus;
'.'  -> $dot;
'|'  -> $alt;

# standard escapes
'\\' -> $esc;
'\^' -> $esc;
'\$' -> $esc;
'\?' -> $esc;
'\*' -> $esc;
'\+' -> $esc;
'\.' -> $esc;
'\|' -> $esc;
'\(' -> $esc; '\)' -> $esc;
'\{' -> $esc; # note no \}
'\[' -> $esc; # note no \]

# whitespace escapes
'\t' -> $esc;
'\n' -> $esc;
'\v' -> $esc;
'\r' -> $esc;
'\f' -> $esc;

# TODO: /./ - the above
/[^$?*+.|(){[\^\\]/ -> $char;

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	# extension escapes
	'\\' -> $esc;
	'\^' -> $esc;
	'\-' -> $esc;

	# whitespace escapes
	'\t' -> $esc;
	'\n' -> $esc;
	'\v' -> $esc;
	'\r' -> $esc;
	'\f' -> $esc;

	# XXX: /./ - the above
	/[^\^\-\\]/ -> $char;
}

'{' -> $opencount .. '}' -> $closecount {
	','      -> $sep;
	/[0-9]+/ -> $count;
}

