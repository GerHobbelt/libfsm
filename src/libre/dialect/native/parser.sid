/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/*
 * TODO: explain choices. e.g. i'm not permitting () or ||. only the root expression is allowed to be empty.
 * TODO: explain strategy: pass in x,y endpoints top-down
 */

%types%

	pos;
	char;
	unsigned;
	!re_flags;
	!err;
	ast_expr;
	ast_class_id;
	ast_count;
	ast_class;
	endpoint;

%terminals%

	ANY;
	!MANY;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	START;
	END;

	OPENSUB;   CLOSESUB;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	NAMED_CLASS: () -> (:ast_class_id, :pos, :pos);

	!OPENFLAGS;
	!CLOSEFLAGS;
	!NEGATE;
	!FLAG_UNKNOWN;
	!FLAG_INSENSITIVE: () -> (:re_flags);

	ESC:      () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	!CONTROL: () -> (:char, :pos, :pos);
	OCT:      () -> (:char, :pos, :pos);
	HEX:      () -> (:char, :pos, :pos);
	CHAR:     () -> (:char, :pos, :pos);
	COUNT:    () -> (:unsigned);

	EOF;
	!ERROR;

%productions%

	!<re-flag-none>:  ()                     -> (:re_flags);
	!<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	<atom-kleene>: () -> (:ast_count);
	<atom-plus>:   () -> (:ast_count);
	<atom-one>:    () -> (:ast_count);
	<atom-opt>:    () -> (:ast_count);
	<atom-count>:  (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	<ast-range-endpoint-literal>: (:char)         -> (:endpoint);
	!<ast-range-endpoint-class>:  (:ast_class_id) -> (:endpoint);
	<ast-range-distinct>: (:endpoint, :pos, :endpoint, :pos) -> ();

	<ast-make-expr-empty>:        ()                       -> (:ast_expr);
	<ast-make-expr-literal>:      (:char)                  -> (:ast_expr);
	<ast-make-expr-concat>:       (:ast_expr, :ast_expr)   -> (:ast_expr);
	<ast-make-expr-alt>:          (:ast_expr, :ast_expr)   -> (:ast_expr);
	<ast-make-expr-any>:          ()                       -> (:ast_expr);
	<ast-make-expr-atom>:         (:ast_expr, :ast_count)  -> (:ast_expr);
	!<ast-make-expr-atom-any>:    (:ast_count)             -> (:ast_expr);
	<ast-make-expr-class>:        (:ast_class, :pos, :pos) -> (:ast_expr);
	<ast-make-expr-group>:        (:ast_expr)              -> (:ast_expr);
	!<ast-make-expr-re-flags>:    (:re_flags, :re_flags)   -> (:ast_expr);
	<ast-make-expr-anchor-start>: ()                       -> (:ast_expr);
	<ast-make-expr-anchor-end>:   ()                       -> (:ast_expr);

	<ast-make-class-concat>:      (:ast_class, :ast_class) -> (:ast_class);
	<ast-make-class-literal>:     (:char)                  -> (:ast_class);
	<ast-make-class-range>:       (:endpoint, :pos, :endpoint, :pos) -> (:ast_class);
	<ast-make-class-named>:       (:ast_class_id)          -> (:ast_class);
	!<ast-make-class-subtract>:   (:ast_class, :ast_class) -> (:ast_class);

	<ast-make-class-flag-none>:   () -> (:ast_class);
	<ast-make-class-flag-invert>: () -> (:ast_class);
	!<ast-make-class-flag-minus>: () -> (:ast_class);
	!<ast-make-class-flag-invert-minus>: () -> (:ast_class);

	<err-expected-term>;
	<err-expected-count>;
	!<err-expected-atoms>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	!<err-expected-groupbody>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	<err-expected-eof>;
	!<err-unsupported>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();

	expr: () -> (node :ast_expr) [
		literal: () -> (node :ast_expr) = {
			{
				(c, !, !) = ESC;
			||
				(c, !, !) = OCT;
			||
				(c, !, !) = HEX;
			||
				(c, !, !) = CHAR;
			};
			node = <ast-make-expr-literal>(c);
  		};

		class: () -> (node :ast_expr) [
			/* TODO: []] [-]] etc.? though they can be escaped. */
			class-head: () -> (f :ast_class) = {
				(!) = INVERT;
				f = <ast-make-class-flag-invert>();
			||
				f = <ast-make-class-flag-none>();
			};

			class-literal: () -> (node :ast_class) = {
				{
					(c, !, !) = ESC;
				||
					(c, !, !) = OCT;
				||
					(c, !, !) = HEX;
				||
					(c, !, !) = CHAR;
				};
				node = <ast-make-class-literal>(c);
	  		};

			class-range: () -> (node :ast_class) = {
				{
					(ca, start, !) = ESC;
				||
					(ca, start, !) = OCT;
				||
					(ca, start, !) = HEX;
				||
					(ca, start, !) = CHAR;
				};
				a = <ast-range-endpoint-literal>(ca);
				
				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};
	
				{
					(cz, !, end) = ESC;
				||
					(cz, !, end) = OCT;
				||
					(cz, !, end) = HEX;
				||
					(cz, !, end) = CHAR;
				||
					(cz, !, end) = RANGE;
				};
				z = <ast-range-endpoint-literal>(cz);
	
				<mark-range>(start, end);
				<ast-range-distinct>(a, start, z, end);
				node = <ast-make-class-range>(a, start, z, end);
			};

			class-named: () -> (id :ast_class_id) = {
                        	(id, !, !) = NAMED_CLASS;
			};

			class-term: () -> (node :ast_class) = {
				node = class-literal();
			||
				node = class-range();
			||
				id = class-named();
				node = <ast-make-class-named>(id);
			##
				<err-expected-term>();
				/* workaround sid warning */
				node = <ast-make-class-flag-none>();
			};
			    
			list-of-class-terms: () -> (node :ast_class) = {
				l = class-term();
				{
					r = list-of-class-terms();
					node = <ast-make-class-concat>(l, r);
				||
					node = l;
				};
			};
		] = {
			(start, !) = OPENGROUP;
			head = class-head();
			body = list-of-class-terms();

			{
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
				end = start; /* appease sid */
			};

			hb = <ast-make-class-concat>(head, body);
			node = <ast-make-expr-class>(hb, start, end);
		};

		atom: () -> (node :ast_expr) = {
			{
				OPENSUB;
				g = expr();
				e = <ast-make-expr-group>(g);
				{
					CLOSESUB;
				##
					<err-expected-alts>;
				};
			||
				e = class();
			||
				e = literal();
			||
				START;
				e = <ast-make-expr-anchor-start>();
			||
				END;
				e = <ast-make-expr-anchor-end>();
			||
				ANY;
				e = <ast-make-expr-any>();
			};

			{
				(start, !) = OPENCOUNT;
				m = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <atom-count>(m, start, m, end);
				node = <ast-make-expr-atom>(e, c);
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				SEP;
				n = COUNT;
				(end, !) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <atom-count>(m, start, n, end);
				node = <ast-make-expr-atom>(e, c);
			||
				OPT;
				c = <atom-opt>();
				node = <ast-make-expr-atom>(e, c);
			||
				STAR;
				c = <atom-kleene>();
				node = <ast-make-expr-atom>(e, c);
			||
				PLUS;
				c = <atom-plus>();
				node = <ast-make-expr-atom>(e, c);
			||
				c = <atom-one>();
				node = <ast-make-expr-atom>(e, c);
			##
				<err-expected-count>;
				node = e;
			};
		};

		list-of-atoms: () -> (node :ast_expr) = {
		/* TODO: start/end anchors */
			a = atom();
			r = <ast-make-expr-empty>;
			node = <ast-make-expr-concat>(a,r);
		||
			a = atom();
			r = list-of-atoms();
			node = <ast-make-expr-concat>(a,r);
		};

		alt: () -> (node :ast_expr) = {
			node = list-of-atoms();
		};

		list-of-alts: () -> (node :ast_expr) = {
			a = alt();
			r = <ast-make-expr-empty>;
			node = <ast-make-expr-alt>(a,r);
	  	||
			a = alt();
			ALT;
			r = list-of-alts();
			node = <ast-make-expr-alt>(a, r);
		##
			<err-expected-alts>;
			node = <ast-make-expr-empty>();
		};
	] = {
		/* don't wrap a single alt in an alt chain */
		node = alt();
	||
		/* keep alts into a linked list -- every right node
		 * is either another alt or empty. */
		a = alt();
		ALT;
		r = list-of-alts();
		node = <ast-make-expr-alt>(a, r);
	##
		<err-expected-alts>;
		node = <ast-make-expr-empty>();
	};

	re_native: () -> (node :ast_expr) = {
		{
			node = expr();
		||
			node = <ast-make-expr-empty>();
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_native;

