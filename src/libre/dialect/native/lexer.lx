# $Id$

'('  -> $opensub;
')'  -> $closesub;

# predicates
'^'  -> $start;
'$'  -> $end;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'.'  -> $dot;
'|'  -> $alt;

# standard escapes
'\\' -> $esc;
'\^' -> $esc;
'\$' -> $esc;
'\?' -> $esc;
'\*' -> $esc;
'\+' -> $esc;
'\.' -> $esc;
'\|' -> $esc;
'\(' -> $esc; '\)' -> $esc;
'\{' -> $esc; # note no \}
'\[' -> $esc; # note no \]

# whitespace escapes
'\f' -> $esc;
'\n' -> $esc;
'\r' -> $esc;
'\t' -> $esc;
'\v' -> $esc;

'\'  /[0-7]+/     -> $oct;
'\x' /[0-9a-f]+/i -> $hex;

# TODO: /./ - the above
/[^$?*+.|(){[\^\\]/ -> $char;

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	# extension escapes
	'\\' -> $esc;
	'\^' -> $esc;
	'\-' -> $esc;

	# whitespace escapes
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;
	'\v' -> $esc;

	'\'  /[0-7]+/     -> $oct;
	'\x' /[0-9a-f]+/i -> $hex;

	# XXX: /./ - the above
	/[^\^\-\\]/ -> $char;
}

'{' -> $opencount .. '}' -> $closecount {
	','      -> $sep;
	/[0-9]+/ -> $count;
}

