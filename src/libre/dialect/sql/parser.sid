/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/*
 * ISO/IEC 9075-2:1999 (E) Â©ISO/IEC
 * 8.6 <similar predicate>
 *
 * https://firebirdsql.org/refdocs/langrefupd25-similar-to.html
 *
 * TODO: SQL99 permits an arbitrary optional escape character
 */

%types%

	pos;
	char;
	unsigned;
	!fsm;
	!fsm_state;
	!grp;
	!pred;
	!re_flags;
	!err;
	ast_expr;
	ast_count;
        ast_class_id;
	char_class_ast;

%terminals%

	ANY;
	MANY;
	OPT;
	PLUS;
	ALT;
	STAR;

	OPENSUB;   CLOSESUB;
	OPENGROUP:  () -> (:pos, :pos); CLOSEGROUP:  () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos);
	COUNT: () -> (:unsigned);
	SEP;
	CLOSECOUNT: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

        !CLASS_alnum:   () -> (:ast_class_id);
	!CLASS_alpha:   () -> (:ast_class_id);
	!CLASS_any:    () -> (:ast_class_id);
	!CLASS_ascii:  () -> (:ast_class_id);
	!CLASS_blank:  () -> (:ast_class_id);
	!CLASS_cntrl:  () -> (:ast_class_id);
	!CLASS_digit:   () -> (:ast_class_id);
	!CLASS_graph:  () -> (:ast_class_id);
	!CLASS_lower:   () -> (:ast_class_id);
	!CLASS_print:  () -> (:ast_class_id);
	!CLASS_punct:  () -> (:ast_class_id);
	!CLASS_space:   () -> (:ast_class_id);
	!CLASS_spchr:   () -> (:ast_class_id);
	!CLASS_upper:   () -> (:ast_class_id);
	!CLASS_word:   () -> (:ast_class_id);
	!CLASS_xdigit: () -> (:ast_class_id);
	!CLASS_ndigit: () -> (:ast_class_id);
	!CLASS_nspace: () -> (:ast_class_id);

	!OPENFLAGS;
	!CLOSEFLAGS;
	!NEGATE;
	!FLAG_UNKNOWN;
	!FLAG_INSENSITIVE: () -> (:re_flags);

	!ESC:     () -> (:char, :pos, :pos);
	!CONTROL: () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	!OCT:     () -> (:char, :pos, :pos);
	!HEX:     () -> (:char, :pos, :pos);
	CHAR:     () -> (:char, :pos, :pos);
	!START:   () -> (:pred);
	!END:     () -> (:pred);

	EOF;
	!ERROR;

%productions%

	!<make-group>:  () -> (:grp);
	!<invert-group>:    (:grp &) -> ();
	!<group-add-char>:  (:grp &, :char) -> ();
	!<group-add-class>: (:grp &, :fsm) -> ();
	!<group-add-range>: (:grp &, :char, :char) -> ();
	!<group-to-states>: (:grp &, :fsm_state, :fsm_state) -> ();

	!<add-concat>:  () -> (:fsm_state);
	!<add-epsilon>: (:fsm_state, :fsm_state)        -> ();
	!<add-pred>:   (:fsm_state, :fsm_state, :pred) -> ();
	!<add-literal>: (:fsm_state, :fsm_state, :char) -> ();
	!<add-any>:     (:fsm_state, :fsm_state)        -> ();
	!<add-class>:  (:fsm_state, :fsm_state, :fsm)  -> ();

	!<count-m-to-n>: (:fsm_state, :fsm_state &, :unsigned, :unsigned) -> ();
	!<count-0-or-1>: (:fsm_state, :fsm_state) -> ();
	!<count-0-or-many>: (:fsm_state, :fsm_state &) -> ();
	!<count-1-or-many>: (:fsm_state, :fsm_state &) -> ();
	!<count-1>: (:fsm_state, :fsm_state) -> ();

	!<push-flags>;
	!<pop-flags>;
	!<set-flag>: (:re_flags) -> ();
	!<clear-flag>: (:re_flags) -> ();

	!<err-expected-term>;
	!<err-expected-count>;
	!<err-expected-atoms>;
	!<err-expected-alts>;
	!<err-expected-range>;
	!<err-expected-closegroup>;
	!<err-unknown-flag>;
	!<err-expected-closeflags>;
	!<err-expected-groupbody>;
	<err-expected-eof>;

	!<mark-group>: (:pos, :pos) -> ();
	!<mark-range>: (:pos, :pos) -> ();
	!<mark-count>: (:pos, :pos) -> ();

        <atom-kleene>: () -> (:ast_count);
        <atom-plus>: () -> (:ast_count);
        <atom-one>: () -> (:ast_count);
        <atom-opt>: () -> (:ast_count);

        <expr-count>: (:unsigned, :unsigned) -> (:ast_count);

	<ast-expr-empty>: () -> (:ast_expr);
	<ast-expr-literal>: (:char) -> (:ast_expr);
	<ast-expr-concat>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-alt>: (:ast_expr, :ast_expr) -> (:ast_expr);
	!<ast-expr-any>: () -> (:ast_expr);
	!<ast-expr-many>: () -> (:ast_expr);

        <ast-expr-atom>: (:ast_expr, :ast_count) -> (:ast_expr);
        <ast-expr-atom-any>: (:ast_count) -> (:ast_expr);
        <ast-expr-atom-many>: (:ast_count) -> (:ast_expr);
	<ast-expr-char-class>: (:char_class_ast) -> (:ast_expr);

	<char-class-ast-literal>: (:char) -> (:char_class_ast);
	<char-class-ast-range>: (:char, :char) -> (:char_class_ast);
	<char-class-ast-concat>: (:char_class_ast, :char_class_ast) -> (:char_class_ast);

	<char-class-ast-flag-none>: () -> (:char_class_ast);
	<char-class-ast-flag-invert>: () -> (:char_class_ast);
	<char-class-ast-flag-minus>: () -> (:char_class_ast);
	<char-class-ast-flag-invert-minus>: () -> (:char_class_ast);

	expr: () -> (n :ast_expr) [
                literal: () -> (n :ast_expr) = {
  			(c, !, !) = CHAR;
			n = <ast-expr-literal>(c);
  		};

		atom-suffix: () -> (f :ast_count) = {
			STAR;
			f = <atom-kleene>();
		||
			PLUS;
			f = <atom-plus>();
		||
			OPT;
			f = <atom-opt>();
		||
			(pos_of, pos_ot) = OPENCOUNT;
			l = COUNT;
			(pos_cf, pos_ct) = CLOSECOUNT;
			f = <expr-count>(l, l);
		||
			(pos_of, pos_ot) = OPENCOUNT;
			l = COUNT;
			SEP;
			h = COUNT;
			(pos_cf, pos_ct) = CLOSECOUNT;
			f = <expr-count>(l, h);
		||
			f = <atom-one>();
		};

	        /* [abc] -> when it reaches the end, make an empty class, and
		 *     each character literal / named class / etc. modifies it.
		 *     
		 * in the case of the SQL dialect's wonky [abc^def], it should
		 * create two groups, and then remove anything from the former
		 * that is in the latter.
		 *
		 * [abc] literals
		 * [a-c] range
		 * [-^] characters only allowed at the head
		 * [-] also allowed at the tail (just [-] is ambiguous)
		 * [[:NAME:]] named character classes
		 *
		 * _ is the ANY character class
		 * */
		char-class: () -> (n :ast_expr) [
			char-class-head: () -> (f :char_class_ast) = {
				(!) = INVERT;
				f = <char-class-ast-flag-invert>();
			||
				(!, !, !) = RANGE;
				f = <char-class-ast-flag-minus>();
			||
				(!) = INVERT;
				(!, !, !) = RANGE;
				f = <char-class-ast-flag-invert-minus>();
			||
				f = <char-class-ast-flag-none>();
			};

			char-class-tail: () -> (f :char_class_ast) = {
				(!, !, !) = RANGE;
				f = <char-class-ast-flag-minus>();
			||
				f = <char-class-ast-flag-none>();
			};

			term: () -> (n :char_class_ast) = {
				(a, !, !) = CHAR;
				(!, !, !) = RANGE;
				(z, !, !) = CHAR;
				n = <char-class-ast-range>(a, z);
			||
				(c, !, !) = CHAR;
				n = <char-class-ast-literal>(c);
			};
			    
			list-of-char-class-terms: () -> (n :char_class_ast) = {
				l = term();
				{
					r = list-of-char-class-terms();
					n = <char-class-ast-concat>(l, r);
				||
					n = l;
				};
			};
			    
			char-class-ast: () -> (n :ast_expr) = {
				head = char-class-head();
				body = list-of-char-class-terms();
				tail = char-class-tail();

				hb = <char-class-ast-concat>(head, body);
				hbt = <char-class-ast-concat>(hb, tail);
				n = <ast-expr-char-class>(hbt);
			};
		] = {
			(!, !) = OPENGROUP;
			n = char-class-ast();
			(!, !, !) = CLOSEGROUP;
		};

		/* TODO: is there a better name than 'atom' for
		 * this construct? it isn't atomic... */
		atom: () -> (n :ast_expr) = {
			OPENSUB;
			e = expr();
			CLOSESUB;
			s = atom-suffix();
			n = <ast-expr-atom>(e, s);
		||
			e = char-class();
			s = atom-suffix();
			n = <ast-expr-atom>(e, s);
		||
			e = literal();
			s = atom-suffix();
			n = <ast-expr-atom>(e, s);
		||
			ANY;
			s = atom-suffix();
			n = <ast-expr-atom-any>(s);
		||
			MANY;
			s = atom-suffix();
			n = <ast-expr-atom-many>(s);
		};

		list-of-atoms: () -> (n :ast_expr) = {
			n = atom();
		||
			a = atom();
			r = list-of-atoms();
			n = <ast-expr-concat>(a,r);
		};

		alt: () -> (n :ast_expr) = {
			n = list-of-atoms();
		};

		list-of-alts: () -> (n :ast_expr) = {
			n = alt();
		||
			a = alt();
			ALT;
			r = list-of-alts();
			n = <ast-expr-alt>(a, r);
		};
	] = {
		n = list-of-alts();
	};

	re_sql: () -> (n :ast_expr) = {
		{
			n = expr();
		##
			n = <ast-expr-empty>();
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_sql;

