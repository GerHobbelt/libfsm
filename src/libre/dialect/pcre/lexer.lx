#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

'(?:'  -> $opensub;
'('  -> $opencapture;
')'  -> $close;

# predicates
'^'  -> $start;
'$'  -> $end;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'.'  -> $any;
'|'  -> $alt;

# standard escapes
'\\' -> $esc;
'\^' -> $esc;
'\$' -> $esc;
'\?' -> $esc;
'\*' -> $esc;
'\+' -> $esc;
'\.' -> $esc;
'\|' -> $esc;
'\(' -> $esc; '\)' -> $esc;
'\{' -> $esc; # note no \}
'\[' -> $esc; # note no \]

# whitespace escapes
'\f' -> $esc;
'\n' -> $esc;
'\r' -> $esc;
'\t' -> $esc;

# perl/unix-ish escapes
'\a' -> $esc;
'\c' /./ -> $control;

# character type escapes
# Note: these need to be their own lexemes distinct from named character
# classes such as /[:digit:]/, as they are allowed in different contexts
char_type = '\' /[dDhHsSvVwWN]/;
char_type -> $char__type;

'\'   /[0-7]{1,3}/     -> $oct;
'\o{' /[0-7]+/i '}'    -> $oct;
'\x'  /[0-9a-f]{1,2}/i -> $hex;
'\x{' /[0-9a-f]+/i '}' -> $hex;

'\' /./ - ($esc | $control | $oct | $hex | '\Q' | char_type) -> $noesc;
'\' -> $invalid;

'(?' -> $openflags .. ')' -> $closeflags {
	'-' -> $negate;
	'i' -> $flag__insensitive;
	/[a-z]/ - $flag__insensitive -> $flag__unknown;
}

'[' -> $opengroup .. ']' -> $closegroup {
	'^' -> $invert;
	'-' -> $range;

	# extension escapes
	'\\' -> $esc;
	'\^' -> $esc;
	'\-' -> $esc;

	# whitespace escapes
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;

	# perl/unix-ish escapes
	'\a' -> $esc;
	'\c' /./ -> $control;

        # other metacharacters that can be escaped within square brackets
        # for consistency's sake, although it isn't strictly necessary
        '\$' -> $esc;
        '\.' -> $esc;
        '\[' -> $esc;
        '\|' -> $esc;
        '\(' -> $esc;
        '\?' -> $esc;
        '\*' -> $esc;
        '\+' -> $esc;
        '\{' -> $esc;

	char_type = '\' /[dDhHsSvVwWN]/;
	char_type -> $char_type;

	'\'   /[0-7]{1,3}/     -> $oct;
	'\o{' /[0-7]+/i '}'    -> $oct;
	'\x'  /[0-9a-f]{1,2}/i -> $hex;
	'\x{' /[0-9a-f]+/i '}' -> $hex;

	'\' /./ - ($esc | $control | $oct | $hex | '\Q' | char_type) -> $noesc;
	'\' -> $invalid;

        # FIXME: negated character classes, e.g. [:^alpha:]
	'[:alnum:]'  -> $class_alnum;
	'[:alpha:]'  -> $class_alpha;
	'[:ascii:]'  -> $class_ascii;
	'[:blank:]'  -> $class_blank;
	'[:cntrl:]'  -> $class_cntrl;
	'[:digit:]'  -> $class_digit;
	'[:graph:]'  -> $class_graph;
	'[:lower:]'  -> $class_lower;
	'[:print:]'  -> $class_print;
	'[:punct:]'  -> $class_punct;
	'[:space:]'  -> $class_space;
	'[:upper:]'  -> $class_upper;
	'[:word:]'   -> $class_word;
	'[:xdigit:]' -> $class_xdigit;

	# XXX: /./ - the above
	/[^\^\-\\]/ -> $char;

	'\Q' .. '\E' {
		/./ -> $char;
	}
}

'{' -> $opencount .. '}' -> $closecount {
	','      -> $sep;
	/[0-9]+/ -> $count;
}

'\Q' .. '\E' {
	/./ -> $char;
}

/./ - ($opensub | $opencapture | $close | $start | $end
	| $opt | $star | $plus | $any | $alt
	| $esc | $oct | $hex | $invalid
	| $opengroup | $opencount) -> $char;

