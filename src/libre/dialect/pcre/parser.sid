/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/* Regular expressions, as evaluated by PCRE, which supports the
 * patterns listed at:
 *     http://pcre.org/current/doc/html/pcre2pattern.html
 *
 * Note that not all regex extensions are supported yet. */

%types%

	pos;
	char;
	unsigned;
	pred;
	re_flags;
	!err;
	ast_expr;
        ast_class_id;
	ast_count;
	char_class_ast;

%terminals%

	ANY;
	!MANY;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	OPENSUB; OPENCAPTURE; CLOSE;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	CLASS_alnum:  () -> (:ast_class_id);
	CLASS_alpha:  () -> (:ast_class_id);
	!CLASS_any:    () -> (:ast_class_id);
	CLASS_ascii:  () -> (:ast_class_id);
	CLASS_blank:  () -> (:ast_class_id);
	CLASS_cntrl:  () -> (:ast_class_id);
	CLASS_digit:  () -> (:ast_class_id);
	CLASS_graph:  () -> (:ast_class_id);
	CLASS_lower:  () -> (:ast_class_id);
	CLASS_print:  () -> (:ast_class_id);
	CLASS_punct:  () -> (:ast_class_id);
	CLASS_space:  () -> (:ast_class_id);
	!CLASS_spchr:  () -> (:ast_class_id);
	CLASS_upper:  () -> (:ast_class_id);
	CLASS_word:   () -> (:ast_class_id);
	CLASS_xdigit: () -> (:ast_class_id);

	!CLASS_ndigit: () -> (:ast_class_id);
	!CLASS_nspace: () -> (:ast_class_id);

	OPENFLAGS;
	CLOSEFLAGS;
	NEGATE;
	FLAG_UNKNOWN;
	FLAG_INSENSITIVE: () -> (:re_flags);

	ESC:     () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	!CONTROL: () -> (:char, :pos, :pos);
	OCT:     () -> (:char, :pos, :pos);
	HEX:     () -> (:char, :pos, :pos);
	CHAR:    () -> (:char, :pos, :pos);
	!START:   () -> (:pred);
	!END:     () -> (:pred);
	COUNT:   () -> (:unsigned);

	EOF;
	!ERROR;

%productions%

	!<push-flags>;
	!<pop-flags>;
	!<set-flag>: (:re_flags) -> ();
	!<clear-flag>: (:re_flags) -> ();

	<err-expected-term>;
	<err-expected-count>;
	!<err-expected-atoms>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-expected-groupbody>;
	<err-unknown-flag>;
	<err-expected-closeflags>;
	<err-expected-eof>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();

	<ast-expr-empty>: () -> (:ast_expr);
	<ast-expr-literal>: (:char) -> (:ast_expr);
	<ast-expr-concat>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-alt>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-any>: () -> (:ast_expr);
	!<ast-expr-many>: () -> (:ast_expr);

        <ast-expr-atom>: (:ast_expr, :ast_count) -> (:ast_expr);
        !<ast-expr-atom-any>: (:ast_count) -> (:ast_expr);
        !<ast-expr-atom-many>: (:ast_count) -> (:ast_expr);
	<ast-expr-char-class>: (:char_class_ast, :pos, :pos) -> (:ast_expr);
        <ast-expr-group>: (:ast_expr) -> (:ast_expr);
	<ast-expr-re-flags>: (:re_flags, :re_flags) -> (:ast_expr);

        <atom-kleene>: () -> (:ast_count);
        <atom-plus>: () -> (:ast_count);
        <atom-one>: () -> (:ast_count);
        <atom-opt>: () -> (:ast_count);

        <expr-count>: (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	<char-class-ast-literal>: (:char) -> (:char_class_ast);
	<char-class-ast-range>: (:char, :pos, :char, :pos) -> (:char_class_ast);
	<char-class-ast-concat>: (:char_class_ast, :char_class_ast) -> (:char_class_ast);
	!<char-class-ast-subtract>: (:char_class_ast, :char_class_ast) -> (:char_class_ast);
	<char-class-ast-named-class>: (:ast_class_id) -> (:char_class_ast);

	<char-class-ast-flag-none>: () -> (:char_class_ast);
	<char-class-ast-flag-invert>: () -> (:char_class_ast);
	!<char-class-ast-flag-minus>: () -> (:char_class_ast);
	!<char-class-ast-flag-invert-minus>: () -> (:char_class_ast);

	<re-flag-none>: () -> (:re_flags);
	<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	expr: () -> (n :ast_expr) [
		literal: () -> (n :ast_expr) = {
			{
				(c, !, !) = ESC;
				||
				(c, !, !) = OCT;
				||
				(c, !, !) = HEX;
				||
				(c, !, !) = CHAR;
			};
			n = <ast-expr-literal>(c);
		};
		    
		char-class: () -> (n :ast_expr) [
			char-class-head: () -> (f :char_class_ast) = {
				(!) = INVERT;
				f = <char-class-ast-flag-invert>();
			||
				f = <char-class-ast-flag-none>();
			};


			class-literal: () -> (n :char_class_ast) = {
				{
					(c, !, !) = ESC;
					||
					(c, !, !) = OCT;
					||
					(c, !, !) = HEX;
					||
					(c, !, !) = CHAR;
				};
				n = <char-class-ast-literal>(c);
	  		};

			class-range: () -> (n :char_class_ast) = {
				{
					(a, start, !) = OCT;
				||
					(a, start, !) = HEX;
				||
					(a, start, !) = CHAR;
				};

				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};
	
				{
					(b, !, end) = OCT;
				||
					(b, !, end) = HEX;
				||
					(b, !, end) = CHAR;
				||
					(b, !, end) = RANGE;
				};
	
				<mark-range>(start, end);
				n = <char-class-ast-range>(a, start, b, end);
			};

			class-named: () -> (k :ast_class_id) = {
				k = CLASS_alnum;
				||
				k = CLASS_alpha;
				||
				k = CLASS_ascii;
				||
				k = CLASS_blank;
				||
				k = CLASS_cntrl;
				||
				k = CLASS_digit;
				||
				k = CLASS_graph;
				||
				k = CLASS_lower;
				||
				k = CLASS_print;
				||
				k = CLASS_punct;
				||
				k = CLASS_space;
				||
				k = CLASS_upper;
				||
				k = CLASS_word;
				||
				k = CLASS_xdigit;
			};

			class-term: () -> (n :char_class_ast) = {
				n = class-literal();
			||
				n = class-range();
			||
				id = class-named();
				n = <char-class-ast-named-class>(id);
			##
				<err-expected-term>;
                                id = CLASS_blank;  /* workaround sid warning */
                                n = <char-class-ast-named-class>(id);
			};

			list-of-char-class-terms: () -> (n :char_class_ast) = {
				l = class-term();
				{
					r = list-of-char-class-terms();
					n = <char-class-ast-concat>(l, r);
				||
					n = l;
				};
			};
		] = {
			(start, !) = OPENGROUP;
			head = char-class-head();
			body = list-of-char-class-terms();

                        {
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
                                end = start; /* appease sid */
			};

			hb = <char-class-ast-concat>(head, body);
			n = <ast-expr-char-class>(hb, start, end);
		};

                flags: () -> (n :ast_expr) [
			flag_set: (i :re_flags) -> (o :re_flags) = {
				c = FLAG_INSENSITIVE;
				o = <re-flag-union>(i, c);
			||
				FLAG_UNKNOWN;
				o = i;
				<err-unknown-flag>;
			};
		] = {
			OPENFLAGS;

			empty_pos = <re-flag-none>();
			empty_neg = <re-flag-none>();

			{
				pos = flag_set(empty_pos);
			||
				pos = empty_pos;
			};

			{
				NEGATE;
				neg = flag_set(empty_neg);
			||
				neg = empty_neg;
			};

			{
				CLOSEFLAGS;
				n = <ast-expr-re-flags>(pos, neg);
			##
				<err-expected-closeflags>;
				n = <ast-expr-empty>();
			};
                };

		atom: () -> (n :ast_expr) = {
			{
				OPENCAPTURE;
				g = expr();
				e = <ast-expr-group>(g);
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				OPENSUB;
				e = expr();
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				e = char-class();
			||
				e = literal();
			||
				e = flags();
			||
				ANY;
				e = <ast-expr-any>();
			};

			{
				(start, !) = OPENCOUNT;
				x = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(x, start, x, end);
				n = <ast-expr-atom>(e, c);
			||
				(start, !) = OPENCOUNT;
				x = COUNT;
				SEP;
				y = COUNT;
				(end, !) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(x, start, y, end);
				n = <ast-expr-atom>(e, c);
			||
				OPT;
				c = <atom-opt>();
				n = <ast-expr-atom>(e, c);
			||
				STAR;
				c = <atom-kleene>();
				n = <ast-expr-atom>(e, c);
			||
				PLUS;
				c = <atom-plus>();
				n = <ast-expr-atom>(e, c);
			||
				c = <atom-one>();
				n = <ast-expr-atom>(e, c);
			##
				<err-expected-count>;
				n = e;
			};

                };

		list-of-atoms: () -> (n :ast_expr) = {

		/* TODO: start/end anchors */

		/* 	p = START;
		 * 	st = <ast-expr-start>();
		 * 	a = atom();
		 * 	n = <ast-expr-concat>(st,a);
		 * || */
			n = atom();
		||
			a = atom();
			r = list-of-atoms();
			n = <ast-expr-concat>(a,r);
		};

		alt: () -> (n :ast_expr) = {
			n = list-of-atoms();
		};

		list-of-alts: () -> (n :ast_expr) = {
			n = alt();
	  	||
			a = alt();
			ALT;
			r = list-of-alts();
			n = <ast-expr-alt>(a, r);
		};
	] = {
	        n = list-of-alts();
	};

	re_pcre: () -> (n :ast_expr) = {
		{
			n = expr();
		##
			n = <ast-expr-empty>();
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_pcre;

