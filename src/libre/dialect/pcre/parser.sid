/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/* Regular expressions, as evaluated by PCRE, which supports the
 * patterns listed at:
 *     http://pcre.org/current/doc/html/pcre2pattern.html
 *
 * Note that not all PCRE regex extensions are supported yet.
 * For unsupported extensions, our intension is to explicitly
 * return an 'unsupported' error, rather than treating the
 * unrecognized syntax incorrectly.
 * */

%types%

	pos;
	char;
	unsigned;
	pred;
	re_flags;
	!err;
	ast_expr;
        ast_char_class_id;
        ast_char_type_id;
	ast_count;
	char_class_ast;
	range_endpoint;

%terminals%

	ANY;
	!MANY;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	OPENSUB; OPENCAPTURE; CLOSE;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	NAMED_CHAR_CLASS: () -> (:ast_char_class_id, :pos, :pos);

	OPENFLAGS;
	CLOSEFLAGS;
	NEGATE;
	FLAG_UNKNOWN;
	FLAG_INSENSITIVE: () -> (:re_flags);

	ESC:     () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	CONTROL: () -> (:char, :pos, :pos);
	OCT:     () -> (:char, :pos, :pos);
	HEX:     () -> (:char, :pos, :pos);
	CHAR:    () -> (:char, :pos, :pos);
	!START:   () -> (:pred);
	!END:     () -> (:pred);
	COUNT:   () -> (:unsigned);
	CHAR_TYPE: () -> (:ast_char_type_id, :pos, :pos);

	EOF;
	!ERROR;

%productions%

	<err-expected-term>;
	<err-expected-count>;
	!<err-expected-atoms>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-expected-groupbody>;
	<err-unknown-flag>;
	<err-expected-closeflags>;
	<err-expected-eof>;
	<err-unsupported>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();

	<ast-expr-empty>: () -> (:ast_expr);
	<ast-expr-literal>: (:char) -> (:ast_expr);
	<ast-expr-concat>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-alt>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-any>: () -> (:ast_expr);
	!<ast-expr-many>: () -> (:ast_expr);

        <ast-expr-atom>: (:ast_expr, :ast_count) -> (:ast_expr);
        !<ast-expr-atom-any>: (:ast_count) -> (:ast_expr);
        !<ast-expr-atom-many>: (:ast_count) -> (:ast_expr);
	<ast-expr-char-class>: (:char_class_ast, :pos, :pos) -> (:ast_expr);
        <ast-expr-group>: (:ast_expr) -> (:ast_expr);
	<ast-expr-re-flags>: (:re_flags, :re_flags) -> (:ast_expr);
	<ast-expr-char-type>: (:ast_char_type_id) -> (:ast_expr);

        <atom-kleene>: () -> (:ast_count);
        <atom-plus>: () -> (:ast_count);
        <atom-one>: () -> (:ast_count);
        <atom-opt>: () -> (:ast_count);

        <expr-count>: (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	<char-class-ast-literal>: (:char) -> (:char_class_ast);
	<char-class-ast-range>: (:range_endpoint, :pos, :range_endpoint, :pos) -> (:char_class_ast);
	<char-class-ast-concat>: (:char_class_ast, :char_class_ast) -> (:char_class_ast);
	!<char-class-ast-subtract>: (:char_class_ast, :char_class_ast) -> (:char_class_ast);
	<char-class-ast-named-class>: (:ast_char_class_id) -> (:char_class_ast);
	<char-class-ast-char-type>: (:ast_char_type_id) -> (:char_class_ast);

	<char-class-ast-flag-none>: () -> (:char_class_ast);
	<char-class-ast-flag-invert>: () -> (:char_class_ast);
	!<char-class-ast-flag-minus>: () -> (:char_class_ast);
	!<char-class-ast-flag-invert-minus>: () -> (:char_class_ast);

	<ast-range-endpoint-literal>: (:char) -> (:range_endpoint);
	<ast-range-endpoint-char-class>: (:ast_char_class_id) -> (:range_endpoint);
	<ast-range-endpoint-char-type>: (:ast_char_type_id) -> (:range_endpoint);

	<re-flag-none>: () -> (:re_flags);
	<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	expr: () -> (n :ast_expr) [
		literal: () -> (n :ast_expr) = {
			{
				(c, !, !) = ESC;
				||
				(c, !, !) = OCT;
				||
				(c, !, !) = HEX;
				||
				(c, !, !) = CHAR;
			};
			n = <ast-expr-literal>(c);
		};
		    
		char-type: () -> (n :ast_expr) = {
			(id, !, !) = CHAR_TYPE;
			n = <ast-expr-char-type>(id);
		};

		char-class: () -> (n :ast_expr) [
			char-class-head: () -> (f :char_class_ast) = {
				(!) = INVERT;
				f = <char-class-ast-flag-invert>();
			||
				f = <char-class-ast-flag-none>();
			};


			class-literal: () -> (n :char_class_ast) = {
				{
					(c, !, !) = ESC;
					||
					(c, !, !) = OCT;
					||
					(c, !, !) = HEX;
					||
					(c, !, !) = CHAR;
					||
					(c, !, !) = CONTROL;
					<err-unsupported>();
				};
				n = <char-class-ast-literal>(c);
	  		};

			class-named: () -> (k :ast_char_class_id, start :pos, end :pos) = {
				(k, start, end) = NAMED_CHAR_CLASS;
			};


			class-char-type: () -> (n :char_class_ast) = {
				(id, !, !) = CHAR_TYPE;
				n = <char-class-ast-char-type>(id);
			};

			class-range: () -> (n :char_class_ast, start :pos, end :pos) [
				range-endpoint-literal: () -> (r :range_endpoint, start :pos, end :pos) = {
					{
						(c, start, end) = OCT;
					||
						(c, start, end) = HEX;
					||
						(c, start, end) = CHAR;
					||
						(c, start, end) = CONTROL;
						<err-unsupported>();
					};
					r = <ast-range-endpoint-literal>(c);
				};

				range-endpoint-char-class: () -> (r :range_endpoint, start :pos, end :pos) = {
					(id, start, end) = class-named();
					r = <ast-range-endpoint-char-class>(id);
				};

				range-endpoint-char-type: () -> (r :range_endpoint, start :pos, end :pos) = {
					(id, start, end) = CHAR_TYPE;
					r = <ast-range-endpoint-char-type>(id);
				};
			] = {
				{
					(lower, l_start, l_end) = range-endpoint-literal();
				||
					(lower, l_start, l_end) = range-endpoint-char-class();
				||
					(lower, l_start, l_end) = range-endpoint-char-type();
				};

				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};
	
				{
					(upper, u_start, u_end) = range-endpoint-literal();
				||
					(upper, u_start, u_end) = range-endpoint-char-class();
				||
					(upper, u_start, u_end) = range-endpoint-char-type();
				||
					/* allow literal '-' as end of range */
					(c, u_start, u_end) = RANGE;
					upper = <ast-range-endpoint-literal>(c);
				};
	
				<mark-range>(l_start, u_end);
				start = l_start;
				end = u_end;
				n = <char-class-ast-range>(lower, l_start, upper, u_end);
			};

			class-term: () -> (n :char_class_ast) = {
				n = class-literal();
			||
				(n, !, !) = class-range();
			||
				n = class-char-type();
			||
				(id, !, !) = class-named();
				n = <char-class-ast-named-class>(id);
			##
				<err-expected-term>;
				n = <char-class-ast-flag-none>(); /* appease sid */
			};

			list-of-char-class-terms: () -> (n :char_class_ast) = {
				l = class-term();
				{
					r = list-of-char-class-terms();
					n = <char-class-ast-concat>(l, r);
				||
					n = l;
				};
			};
		] = {
			(start, !) = OPENGROUP;
			head = char-class-head();
			body = list-of-char-class-terms();

                        {
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
                                end = start; /* appease sid */
			};

			hb = <char-class-ast-concat>(head, body);
			n = <ast-expr-char-class>(hb, start, end);
		};

                flags: () -> (n :ast_expr) [
			flag_set: (i :re_flags) -> (o :re_flags) = {
				c = FLAG_INSENSITIVE;
				o = <re-flag-union>(i, c);
			||
				FLAG_UNKNOWN;
				o = i;
				<err-unknown-flag>;
			};
		] = {
			OPENFLAGS;

			empty_pos = <re-flag-none>();
			empty_neg = <re-flag-none>();

			{
				pos = flag_set(empty_pos);
			||
				pos = empty_pos;
			};

			{
				NEGATE;
				neg = flag_set(empty_neg);
			||
				neg = empty_neg;
			};

			{
				CLOSEFLAGS;
				n = <ast-expr-re-flags>(pos, neg);
			##
				<err-expected-closeflags>;
				n = <ast-expr-empty>();
			};
                };

		atom: () -> (n :ast_expr) = {
			{
				OPENCAPTURE;
				g = expr();
				e = <ast-expr-group>(g);
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				OPENSUB;
				e = expr();
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				e = char-class();
			||
				e = char-type();
			||
				e = literal();
			||
				e = flags();
			||
				ANY;
				e = <ast-expr-any>();
			||
				(!, !, !) = CONTROL;
				<err-unsupported>();
				e = <ast-expr-empty>(); /* appease sid */
			};

			{
				(start, !) = OPENCOUNT;
				x = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(x, start, x, end);
				n = <ast-expr-atom>(e, c);
			||
				(start, !) = OPENCOUNT;
				x = COUNT;
				SEP;
				y = COUNT;
				(end, !) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(x, start, y, end);
				n = <ast-expr-atom>(e, c);
			||
				OPT;
				c = <atom-opt>();
				n = <ast-expr-atom>(e, c);
			||
				STAR;
				c = <atom-kleene>();
				n = <ast-expr-atom>(e, c);
			||
				PLUS;
				c = <atom-plus>();
				n = <ast-expr-atom>(e, c);
			||
				c = <atom-one>();
				n = <ast-expr-atom>(e, c);
			##
				<err-expected-count>;
				n = e;
			};

                };

		list-of-atoms: () -> (n :ast_expr) = {

		/* TODO: start/end anchors */

		/* 	p = START;
		 * 	st = <ast-expr-start>();
		 * 	a = atom();
		 * 	n = <ast-expr-concat>(st,a);
		 * || */
			n = atom();
		||
			a = atom();
			r = list-of-atoms();
			n = <ast-expr-concat>(a,r);
		};

		alt: () -> (n :ast_expr) = {
			n = list-of-atoms();
		};

		list-of-alts: () -> (n :ast_expr) = {
			n = alt();
		||
			/* Explicitly allow e.g. (xyz|) */
			n = <ast-expr-empty>();
	  	||
			a = alt();
			ALT;
			r = list-of-alts();
			n = <ast-expr-alt>(a, r);
		};
	] = {
	        n = list-of-alts();
	};

	re_pcre: () -> (n :ast_expr) = {
		{
			n = expr();
		||
			n = <ast-expr-empty>();
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_pcre;

