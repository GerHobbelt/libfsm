/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

/* Regular expressions, as evaluated by PCRE, which supports the
 * patterns listed at:
 *     http://pcre.org/current/doc/html/pcre2pattern.html
 *
 * Note that not all PCRE regex extensions are supported yet.
 * For unsupported extensions, our intension is to explicitly
 * return an 'unsupported' error, rather than treating the
 * unrecognized syntax incorrectly.
 * */

%types%

	pos;
	char;
	unsigned;
	!pred;
	re_flags;
	!err;
	ast_expr;
	ast_class_id;
	ast_count;
	ast_class;
	range_endpoint;

%terminals%

	ANY;
	!MANY;
	OPT;
	PLUS;
	ALT;
	SEP;
	STAR;

	START;
	END;

	OPENSUB; OPENCAPTURE; CLOSE;
	OPENGROUP: () -> (:pos, :pos); CLOSEGROUP: () -> (:char, :pos, :pos);
	OPENCOUNT: () -> (:pos, :pos); CLOSECOUNT: () -> (:pos, :pos);

	INVERT: () -> (:char);
	RANGE:  () -> (:char, :pos, :pos);

	NAMED_CLASS: () -> (:ast_class_id, :pos, :pos);

	OPENFLAGS;
	CLOSEFLAGS;
	NEGATE;
	FLAG_UNKNOWN;
	FLAG_INSENSITIVE: () -> (:re_flags);

	ESC:     () -> (:char, :pos, :pos);
	!NOESC:   () -> (:char, :pos, :pos);
	CONTROL: () -> (:char, :pos, :pos);
	OCT:     () -> (:char, :pos, :pos);
	HEX:     () -> (:char, :pos, :pos);
	CHAR:    () -> (:char, :pos, :pos);
	COUNT:   () -> (:unsigned);

	EOF;
	!ERROR;

%productions%

	<err-expected-term>;
	<err-expected-count>;
	!<err-expected-atoms>;
	<err-expected-alts>;
	<err-expected-range>;
	<err-expected-closegroup>;
	!<err-expected-groupbody>;
	<err-unknown-flag>;
	<err-expected-closeflags>;
	<err-expected-eof>;
	<err-unsupported>;

	<mark-group>: (:pos, :pos) -> ();
	<mark-range>: (:pos, :pos) -> ();
	<mark-count>: (:pos, :pos) -> ();

	<ast-expr-empty>: () -> (:ast_expr);
	<ast-expr-literal>: (:char) -> (:ast_expr);
	<ast-expr-concat>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-alt>: (:ast_expr, :ast_expr) -> (:ast_expr);
	<ast-expr-any>: () -> (:ast_expr);

        <ast-expr-atom>: (:ast_expr, :ast_count) -> (:ast_expr);
        !<ast-expr-atom-any>: (:ast_count) -> (:ast_expr);
	<ast-expr-class>: (:ast_class, :pos, :pos) -> (:ast_expr);
        <ast-expr-group>: (:ast_expr) -> (:ast_expr);
	<ast-expr-re-flags>: (:re_flags, :re_flags) -> (:ast_expr);
	<ast-expr-anchor-start>: () -> (:ast_expr);
	<ast-expr-anchor-end>: () -> (:ast_expr);

        <atom-kleene>: () -> (:ast_count);
        <atom-plus>: () -> (:ast_count);
        <atom-one>: () -> (:ast_count);
        <atom-opt>: () -> (:ast_count);

        <expr-count>: (:unsigned, :pos, :unsigned, :pos) -> (:ast_count);

	<ast-class-literal>: (:char) -> (:ast_class);
	!<ast-class-range-distinct>: (:range_endpoint, :pos, :range_endpoint, :pos) -> ();
	<ast-class-range>: (:range_endpoint, :pos, :range_endpoint, :pos) -> (:ast_class);
	<ast-class-concat>: (:ast_class, :ast_class) -> (:ast_class);
	!<ast-class-subtract>: (:ast_class, :ast_class) -> (:ast_class);
	<ast-class-named-class>: (:ast_class_id) -> (:ast_class);

	<ast-class-flag-none>: () -> (:ast_class);
	<ast-class-flag-invert>: () -> (:ast_class);
	!<ast-class-flag-minus>: () -> (:ast_class);
	!<ast-class-flag-invert-minus>: () -> (:ast_class);

	<ast-range-endpoint-literal>: (:char) -> (:range_endpoint);
	<ast-range-endpoint-class>: (:ast_class_id) -> (:range_endpoint);

	<re-flag-none>: () -> (:re_flags);
	<re-flag-union>: (:re_flags, :re_flags) -> (:re_flags);

	expr: () -> (node :ast_expr) [
		literal: () -> (node :ast_expr) = {
			{
				(c, !, !) = ESC;
				||
				(c, !, !) = OCT;
				||
				(c, !, !) = HEX;
				||
				(c, !, !) = CHAR;
			};
			node = <ast-expr-literal>(c);
		};
		    
		class: () -> (node :ast_expr) [
			class-head: () -> (f :ast_class) = {
				(!) = INVERT;
				f = <ast-class-flag-invert>();
			||
				f = <ast-class-flag-none>();
			};


			class-literal: () -> (node :ast_class) = {
				{
					(c, !, !) = ESC;
					||
					(c, !, !) = OCT;
					||
					(c, !, !) = HEX;
					||
					(c, !, !) = CHAR;
					||
					(c, !, !) = CONTROL;
					<err-unsupported>();
				};
				node = <ast-class-literal>(c);
	  		};

			class-named: () -> (k :ast_class_id, start :pos, end :pos) = {
				(k, start, end) = NAMED_CLASS;
			};


			class-range: () -> (node :ast_class, start :pos, end :pos) [
				range-endpoint-literal: () -> (r :range_endpoint, start :pos, end :pos) = {
					{
						(c, start, end) = ESC;
					||
						(c, start, end) = OCT;
					||
						(c, start, end) = HEX;
					||
						(c, start, end) = CHAR;
					||
						(c, start, end) = CONTROL;
						<err-unsupported>();
					};
					r = <ast-range-endpoint-literal>(c);
				};

				range-endpoint-class: () -> (r :range_endpoint, start :pos, end :pos) = {
					(id, start, end) = class-named();
					r = <ast-range-endpoint-class>(id);
				};
			] = {
				{
					(lower, l_start, l_end) = range-endpoint-literal();
				||
					(lower, l_start, l_end) = range-endpoint-class();
				};

				{
					(!, !, !) = RANGE;
				##
					<err-expected-range>;
				};
	
				{
					(upper, u_start, u_end) = range-endpoint-literal();
				||
					(upper, u_start, u_end) = range-endpoint-class();
				||
					/* allow literal '-' as end of range */
					(c, u_start, u_end) = RANGE;
					upper = <ast-range-endpoint-literal>(c);
				};
	
				<mark-range>(l_start, u_end);
				start = l_start;
				end = u_end;
				node = <ast-class-range>(lower, l_start, upper, u_end);
			};

			class-term: () -> (node :ast_class) = {
				node = class-literal();
			||
				(node, !, !) = class-range();
			||
				(id, !, !) = class-named();
				node = <ast-class-named-class>(id);
			##
				<err-expected-term>;
				node = <ast-class-flag-none>(); /* appease sid */
			};

			list-of-class-terms: () -> (node :ast_class) = {
				l = class-term();
				{
					r = list-of-class-terms();
					node = <ast-class-concat>(l, r);
				||
					node = l;
				};
			};
		] = {
			(start, !) = OPENGROUP;
			head = class-head();
			body = list-of-class-terms();

                        {
				(!, !, end) = CLOSEGROUP;
				<mark-group>(start, end);
			##
				<err-expected-closegroup>;
                                end = start; /* appease sid */
			};

			hb = <ast-class-concat>(head, body);
			node = <ast-expr-class>(hb, start, end);
		};

		type: () -> (node :ast_expr) = {
				(id, start, end) = NAMED_CLASS;
				named = <ast-class-named-class>(id);
				node = <ast-expr-class>(named, start, end);
		};
			
                flags: () -> (node :ast_expr) [
			flag_set: (i :re_flags) -> (o :re_flags) = {
				c = FLAG_INSENSITIVE;
				o = <re-flag-union>(i, c);
			||
				FLAG_UNKNOWN;
				o = i;
				<err-unknown-flag>;
			};
		] = {
			OPENFLAGS;

			empty_pos = <re-flag-none>();
			empty_neg = <re-flag-none>();

			{
				pos = flag_set(empty_pos);
			||
				pos = empty_pos;
			};

			{
				NEGATE;
				neg = flag_set(empty_neg);
			||
				neg = empty_neg;
			};

			{
				CLOSEFLAGS;
				node = <ast-expr-re-flags>(pos, neg);
			##
				<err-expected-closeflags>;
				node = <ast-expr-empty>();
			};
                };

		atom: () -> (node :ast_expr) = {
			/* Note that START and END can appear embedded
			 * within a list of atoms. Whether that makes
			 * the regex inherently unmatchable depends on
			 * whether the surrounding context is nullable
			 * or not, so that's detected after parsing has
			 * finished. */
			{
				OPENCAPTURE;
				g = expr();
				e = <ast-expr-group>(g);
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				OPENSUB;
				e = expr();
                                {
					CLOSE;
                                ##
                        		<err-expected-alts>;
				};
			||
				e = class();
			||
				e = type();
			||
				e = literal();
			||
				e = flags();
			||
				START;
				e = <ast-expr-anchor-start>();
			||
				END;
				e = <ast-expr-anchor-end>();
			||
				ANY;
				e = <ast-expr-any>();
			||
				(!, !, !) = CONTROL;
				<err-unsupported>();
				e = <ast-expr-empty>(); /* appease sid */
			};

			{
				(start, !) = OPENCOUNT;
				m = COUNT;
				(!, end) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(m, start, m, end);
				node = <ast-expr-atom>(e, c);
			||
				(start, !) = OPENCOUNT;
				m = COUNT;
				SEP;
				n = COUNT;
				(end, !) = CLOSECOUNT;
				<mark-count>(start, end);
				c = <expr-count>(m, start, n, end);
				node = <ast-expr-atom>(e, c);
			||
				OPT;
				c = <atom-opt>();
				node = <ast-expr-atom>(e, c);
			||
				STAR;
				c = <atom-kleene>();
				node = <ast-expr-atom>(e, c);
			||
				PLUS;
				c = <atom-plus>();
				node = <ast-expr-atom>(e, c);
			||
				c = <atom-one>();
				node = <ast-expr-atom>(e, c);
			##
				<err-expected-count>;
				node = e;
			};

                };

		list-of-atoms: () -> (node :ast_expr) = {
			a = atom();
			r = <ast-expr-empty>;
			node = <ast-expr-concat>(a,r);
		||
			a = atom();
			r = list-of-atoms();
			node = <ast-expr-concat>(a,r);
		};

		alt: () -> (node :ast_expr) = {
			node = list-of-atoms();
		};

		list-of-alts: () -> (node :ast_expr) = {
			a = alt();
			r = <ast-expr-empty>;
			node = <ast-expr-alt>(a,r);
		||
			/* Explicitly allow e.g. (xyz|) */
			a = <ast-expr-empty>;
			r = <ast-expr-empty>;
			node = <ast-expr-alt>(a,r);
	  	||
			a = alt();
			ALT;
			r = list-of-alts();
			node = <ast-expr-alt>(a, r);
		##
		        node = <ast-expr-empty>();
		};
	] = {
		/* don't wrap a single alt in an alt chain */
		node = alt();
	||
		/* keep alts into a linked list -- every right node
		 * is either another alt or empty. */
		a = alt();
		ALT;
		r = list-of-alts();
		node = <ast-expr-alt>(a, r);
	##
		<err-expected-alts>;
		node = <ast-expr-empty>();
	};

	re_pcre: () -> (node :ast_expr) = {
		{
			node = expr();
		};

		{
			EOF;
		##
			<err-expected-eof>;
		};
	};

%entry%

	re_pcre;

