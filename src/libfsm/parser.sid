/*
 * Copyright 2008-2017 Katherine Flavel
 *
 * See LICENCE for the full copyright terms.
 */

%types%

	char;
	string;
	state;

%terminals%

	IDENT: () -> (:string);

	ESC:  () -> (:char);
	OCT:  () -> (:char);
	HEX:  () -> (:char);
	CHAR: () -> (:char);
	LABEL;

	START;
	END;

	TO;
	ANY;
	SEP;
	COMMA;

	EOF;
	!UNKNOWN;

%productions%

	<add-state>:        (:string) -> (:state);
	<mark-start>:       (:state)  -> ();
	<mark-end>:         (:state)  -> ();

	<free>:             (:string) -> ();
	<free-statelist>;

	<add-edge-literal>: (:state, :state, :char) -> ();
	<add-edge-any>:     (:state, :state)        -> ();
	<add-edge-epsilon>: (:state, :state)        -> ();

	<err-expected-sep>;
	<err-expected-trans>;
	<err-expected-comma>;
	<err-expected-start>;
	<err-expected-end>;
	<err-syntax>;

	label: () -> (c :char) = {
		{
			c = ESC;
		||
			c = OCT;
		||
			c = HEX;
		||
			c = CHAR;
		};
		LABEL;
	};

	ident: () -> (n :string) = {
		n = IDENT;
	};

	edge = {
		a = ident;
		TO;
		b = ident;

		x = <add-state>(a);
		y = <add-state>(b);

		<free>(a);
		<free>(b);

		{
			c = label;
			<add-edge-literal>(x, y, c);
		||
			ANY;
			<add-edge-any>(x, y);
		||
			<add-edge-epsilon>(x, y);
		##
			<err-expected-trans>;
		};

		{
			SEP;
		##
			<err-expected-sep>;
		};
	};

	/*
	 * Declaration for a single state, just as a way to force ordering
	 * when states are created. This is a workaround for the .fsm syntax
	 * being so terrible, and that state labels aren't numeric, but the
	 * .fsm format is expected to maintain the same numbering when parsing
	 * its own output.
	 */
	decl = {
		a = ident;

		! = <add-state>(a);

		<free>(a);

		{
			SEP;
		##
			<err-expected-sep>;
		};
	};

	items = {
		items;
		{
			edge;
		||
			decl;
		};
	||
		$;
	};

	xstart = {
		{
			START;
		##
			<err-expected-start>;
		};

		n = ident;

		{
			SEP;
		##
			<err-expected-sep>;
		};

		s = <add-state>(n);
		<mark-start>(s);

		<free>(n);
	||
		$;
	};

	xend [

		end-state = {
			n = ident;

			s = <add-state>(n);
			<mark-end>(s);

			<free>(n);
		};

		end-states = {
			end-state;

			{
				{
					COMMA;
				##
					<err-expected-comma>;
				};

				end-states;
			||
				$;
			};
		};

	] = {
		{
			END;
		##
			<err-expected-end>;
		};

		end-states;

		{
			SEP;
		##
			<err-expected-sep>;
		};
	||
		$;
	};

	fsm = {
		items;
		xstart;
		xend;

		EOF;

		<free-statelist>;
	##
		<err-syntax>;
	};

%entry%

	fsm;

