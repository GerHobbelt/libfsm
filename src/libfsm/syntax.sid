/* $Id$ */

%types%

	int;
	string;
	state;

%terminals%

	id: () -> (:int);
	label: () -> (:string);

	start;
	end;

	arrow;

	sep;
	comma;

	eof;
	!unknown;

%productions%

	<add-state>: (:int) -> (:state);
	<mark-start>: (:int) -> ();
	<mark-end>: (:int) -> ();
	<add-edge>: (:state, :state, :string) -> ();
	<epsilon>: () -> (:string);

	<err-expected-sep>;
	<err-expected-comma>;
	<err-expected-start>;
	<err-expected-end>;
	<err-parse>;


	ids = {
		n = id;
		<mark-end>(n);
	||
		ids;
		{
			comma;
		##
			<err-expected-comma>;
		};
		n = id;
		<mark-end>(n);
	};

	xend = {
		{
			end;
		##
			<err-expected-end>;
		};

		ids;

		{
			sep;
		##
			<err-expected-sep>;
		};
	};

	xstart: () -> (n :int) = {
		{
			start;
		##
			<err-expected-start>;
		};

		n = id;

		{
			sep;
		##
			<err-expected-sep>;
		};
	};

	edge = {
		a = id;
		arrow;
		b = id;

		{
			c = label;
		##
			c = <epsilon>;
		};

		{
			sep;
		##
			<err-expected-sep>;
		};

		x = <add-state>(a);
		y = <add-state>(b);
		<add-edge>(x, y, c);
	};

	edges = {
		edges;
		edge;
	||
		edge;
	};

	fsm = {
		edges;

		n = xstart;
		<mark-start>(n);

		{
			xend;
		||
			$;
		};

		eof;
	##
		<err-parse>;
	};

%entry%

	fsm;

