/* $Id$ */

%types%

	char;
	string;
	state;

%terminals%

	IDENT: () -> (:string);

	LABEL;
	CHAR: () -> (:char);

	START;
	END;

	TO;
	ANY;
	SEP;
	COMMA;

	EOF;
	!UNKNOWN;

%productions%

	<label-char>:       (:char) -> ();
	<label-buffer>:     ()      -> (:string);

	<add-state>:        (:string) -> (:state);
	<mark-start>:       (:state)  -> ();
	<mark-end>:         (:state)  -> ();

	!<free>:            (:string) -> ();
	<free-statelist>;

	<add-edge-literal>: (:state, :state, :string) -> ();
	<add-edge-any>:     (:state, :state)          -> ();
	<add-edge-epsilon>: (:state, :state)          -> ();

	<err-expected-sep>;
	<err-expected-trans>;
	<err-expected-comma>;
	<err-expected-start>;
	<err-expected-end>;
	<err-syntax>;

	label: () -> (n :string) [

		body = {
			c = CHAR;
			<label-char>(c);
			{
				body;
			||
				$;
			};
		};

	] = {
		body;
		LABEL;
		n = <label-buffer>;
	};

	id: () -> (n :string) = {
		n = IDENT;
	||
		n = label;
	};

	edges [

		edge = {
			a = id;
			TO;
			b = id;

			x = <add-state>(a);
			y = <add-state>(b);

			{
				s = label;
				<add-edge-literal>(x, y, s);
			||
				ANY;
				<add-edge-any>(x, y);
			||
				<add-edge-epsilon>(x, y);
			##
				<err-expected-trans>;
			};

			{
				SEP;
			##
				<err-expected-sep>;
			};
		};

	] = {
		edges;
		edge;
	||
		$;
	};

	xstart = {
		{
			START;
		##
			<err-expected-start>;
		};

		n = id;

		{
			SEP;
		##
			<err-expected-sep>;
		};

		s = <add-state>(n);
		<mark-start>(s);
	||
		$;
	};

	xend [

		end-id = {
			n = id;

			s = <add-state>(n);
			<mark-end>(s);
		};

		end-ids = {
			end-id;

			{
				{
					COMMA;
				##
					<err-expected-comma>;
				};

				end-ids;
			||
				$;
			};
		};

	] = {
		{
			END;
		##
			<err-expected-end>;
		};

		end-ids;

		{
			SEP;
		##
			<err-expected-sep>;
		};
	||
		$;
	};

	fsm = {
		edges;
		xstart;
		xend;

		EOF;

		<free-statelist>;
	##
		<err-syntax>;
	};

%entry%

	fsm;

